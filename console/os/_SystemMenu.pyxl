_SystemMenu
════════════════════════════════════════════════════════════════════════

const windowBackgroundColor = #135
const windowBorderColor     = #fff
const textColor             = #fff
const textShadowColor       = #0006
const promptColor           = #4DF
const selectedColor         = #f
const unselectedColor       = #b

const fadeInFrames          = 10
const slideInFrames         = 9

let choiceArray
let choiceIndex = 0

let oldTransform
let oldClip
let oldPostEffects
let maxChoiceWidth
let lineHeight

let font = _font9
if screenSize.y <= 64: font = _font5
else if screenSize.y <= 112: font = _font8


def close(callback):
   // TODO: run closing animation
   popMode(callback)
   

def choose():
   const choice = choiceArray[choiceIndex]

   if choice.confirm:
      pushMode(_ConfirmDialog, choice)
   else:
      close(choice.callback)

      
def setControls():
   if screenSize.x == 64:
      pushMode(_SetControls64)
   else:
      pushMode(_SetControls)


      
enter
────────────────────────────────────────────────────────────────────────
choiceIndex = 0

oldTransform = getTransform()
oldClip = getClip()
oldPostEffects = getPostEffects()

resetPostEffects()
setClip(xy(0, 0), screenSize, -2047, 4096)
// Force the clipping range beyond what the API normally allows
_clipZ2 = 2049


choiceArray = [{text: "Resume Game", callback: nil, confirm: false},
               // Insert custom menu choices after "Resume"
               ..._customPauseMenuOptions,
               {text: "Set Controls", callback: setControls, confirm: false},
               {text: "Restart Game", callback: resetGame, confirm: true},
               {text: "Quit to System", callback: quitGame, confirm: true}]

maxChoiceWidth = 0

lineHeight = round(110% font.lineHeight)
if screenSize.y <= 64: ++lineHeight

for c in choiceArray: maxChoiceWidth = max(maxChoiceWidth, textWidth(font, c.text))



leave
────────────────────────────────────────────────────────────────────────
setTransform(oldTransform)
setClip(oldClip)
setPostEffects(oldPostEffects)



popMode(callback) from _ConfirmDialog
────────────────────────────────────────────────────────────────────────
// Return with the callback
if callback: close(callback)



frame
────────────────────────────────────────────────────────────────────────
if modeFrames > fadeInFrames + slideInFrames + 2:
   for control in pad:
      if control.qq or control._pp:
         // Do NOT use "bb", which maps to enter on the keyboard
         // and may make players think they are executing a command
         playAudioClip(_cancelUISound)
         close(nil)
      else if control.aa:
         playAudioClip(_acceptUISound)
         choose()
      else if control.yy:
         const prev = choiceIndex
         choiceIndex = clamp(choiceIndex + control.yy, 0, size(choiceArray) - 1)
         playAudioClip(if prev == choiceIndex then _denyUISound else _moveUISound)

if modeFrames == fadeInFrames: playAudioClip(_openUISound)

// Draw the paused game shadowed
resetTransform()
drawPreviousMode()

// Draw *at* the near plane, which due to the epsilon z offsets
// guarantees that the menu draws in front of everything from the
// previous mode. This forces us to draw in order, however.
setTransform(xy(0, 0), xy(1, 1), 2048.5, 1)
drawCornerRect(xy(0, 0), screenSize, rgba(14%, 14%, 14%, 80% clamp((modeFrames + 1) / fadeInFrames, 15%, 100%)))


// Our last menu item is designed to be the longest, so use that
// to estimate width
const windowSize = round(xy(maxChoiceWidth + 1.5 font.lineHeight, screenSize.y + 2))

// Animate sliding in of window
setTransform(xy(1 + screenSize.x - windowSize.x * (50% - clamp(1 - (modeFrames - fadeInFrames) / slideInFrames, 0%, 100%)), screenSize.y / 2), xy(1, 1), 2048.5, 1)

// Window shadow
local:
   const P = xy(0, windowSize.y / 2);
   const s = windowSize.x / 2 - 1
   drawLine(P + xy(-s, 0), P + xy(s, 0), rgba(0, 0, 0, 40%))
   drawLine(P + xy(-s+1, 1), P + xy(s-1, 1), rgba(0, 0, 0, 15%))

// Window
drawRect(xy(0, 0), windowSize, windowBackgroundColor, windowBorderColor)

let y = -lineHeight * (size(choiceArray) - 1) / 2

// Draw options
for i < size(choiceArray):
   const active      = choiceIndex == i
   const bright      = (1 + cos(modeFrames / 9)) / 2
   const color       = if active then lerp(promptColor, #fff, 50% bright) else unselectedColor
   const outline     = if active then rgba(1,1,1, 25% bright) else nil
   const yOffset     = if active then -1 else 0
   const shadow      = if active then 50% textShadowColor else textShadowColor

   const x           = -(maxChoiceWidth - 0.6 lineHeight + 1) / 2
   const text        = choiceArray[i].text
   drawText(font, text, xy(x, y + 1), shadow, nil, nil, "left", "center")
   drawText(font, text, xy(x, y + yOffset), color, nil, outline, "left", "center")
   
   if active:
      drawText(font, "✜", xy(x - 40% lineHeight + 1, y + 1), shadow, nil, nil, "right", "center")
      drawText(font, "✜", xy(x - 40% lineHeight + 1, y + yOffset), color, nil, outline, "right", "center")
      
   y += lineHeight
