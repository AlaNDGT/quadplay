_SetControls
════════════════════════════════════════════════════════════════════════

// This code handles all screen sizes. Most use the same layout
// with slight alignment tweaks and font changes in order to
// fit well.
//
// The user interface at 64x64 has a different layout,
// which shows only one player's controls and is preceeded
// by a menu for selecting which player is active.

const windowBackgroundColor = #135
const windowBorderColor     = #fff
const text_color             = #fff
const text_shadow_color       = #0006
const promptColor           = #4DF
const selectedColor         = #f
const unselectedColor       = #b

const controllerSpritesheet = if SCREEN_SIZE.y >= 224 then _controllerSpritesheet44 else _controllerSpritesheet22
const controllerTypeArray = ["Quadplay", "XboxOne", "Xbox360", "Keyboard", "Kbd_Alt", "Kbd_P1", "Kbd_P2", "PS4", "PS3", "SNES", "SwitchPro", "JoyCon_R", "JoyCon_L", "Zero", "HOTAS", "Stadia", "GPD_Win"];

// Tweak layout for square aspect ratios. shiftY is ignored at 64x64
const shiftY = if SCREEN_SIZE.x == SCREEN_SIZE.y then -10 else 0
let shiftSpriteY = if SCREEN_SIZE.x == SCREEN_SIZE.y then -36 else 0
if SCREEN_SIZE.x == 64: shiftSpriteY = -34

// Position of the scroll arrows
const arrowX = if SCREEN_SIZE.x == SCREEN_SIZE.y then 54 else 76
const arrowY = if SCREEN_SIZE.x == 128 then 0 else -2


let font = _font9
if SCREEN_SIZE.y <= 64: font = _font5
else if SCREEN_SIZE.y <= 128: font = _font8

let controlsFont = font
if SCREEN_SIZE.y <= 64: font = _font5
else if SCREEN_SIZE.y <= 128: controlsFont = _font6

let choiceFont = _font9
if SCREEN_SIZE.y <= 64 or SCREEN_SIZE.x <= 128: choiceFont = _font5
else if SCREEN_SIZE.y <= 112: choiceFont = _font8

// For layout
const scale = if SCREEN_SIZE.y > 128 then 100% else 50%

const playerName = []
for p < 4: push(playerName, "P" + (p + 1) + ": ")


// The player to show controls for. Only used at 64x64
let activeIndex = nil

let oldBackground


enter(p)
────────────────────────────────────────────────────────────────────────
activeIndex = p
oldBackground = get_background()



leave
────────────────────────────────────────────────────────────────────────
set_background(oldBackground)



frame
────────────────────────────────────────────────────────────────────────

set_background(windowBackgroundColor)

if SCREEN_SIZE.x == 64:
   draw_sprite(_controlDoneSprite, xy(70, 53))
   draw_sprite(_controlDoneSprite, xy(-6, 60))
else:
   draw_line(xy(SCREEN_SIZE.x / 2 - 1, 0), xy(SCREEN_SIZE.x / 2 - 1, SCREEN_SIZE.y), #023)
   if SCREEN_SIZE.x == 384:
      draw_line(xy(0, SCREEN_SIZE.y / 2), xy(120, SCREEN_SIZE.y / 2), #023)
      draw_line(xy(SCREEN_SIZE.x - 121, SCREEN_SIZE.y / 2), xy(SCREEN_SIZE.x, SCREEN_SIZE.y / 2), #023)

   // "Press P when done"
   // Shift over at 128x128 to crop the word "Press"
   draw_sprite(_controlDoneSprite, SCREEN_SIZE / 2 - (if SCREEN_SIZE.x == 128 then xy(10, 0) else xy(0, 0)))
   if SCREEN_SIZE.x == 128: draw_rect(xy(3, SCREEN_SIZE.y / 2), xy(4, 5), windowBackgroundColor)

   
for p < 4:
   // In 64x64 mode, skip the non-active players
   if activeIndex != nil and p != activeIndex: continue
   
   preserving_transform:
      const control = gamepad_array[p]

      if control._pp or control.qq:
         pop_mode() because "Pressed P or Q"

      if control.xx:
         let i = find(controllerTypeArray, control.type)
         let tryAgain = true
         let type
         while tryAgain:
            i = loop(i + control.xx, size(controllerTypeArray))
            type = controllerTypeArray[i]

            // Force to continue cycling if an illegal value was chosen for
            // this player
            tryAgain = ((p == 0 and type == "Kbd_P2") or
                        ((find(type, "Kbd_") != nil or find(type, "Keyboard") != nil) and
                         ((p > 1) or
                          (p == 1 and (type != "Kbd_P2")))))
         // Switch the device to the selected controller
         device_control("setPadType", p, type)

      if SCREEN_SIZE.x == 64:
         set_transform(xy(32, 6), xy(1, 1))
      else:
         set_transform(xy((0.5 + (p bitand 1)) * SCREEN_SIZE.x / 2,
                         (p bitshr 1) * SCREEN_SIZE.y / 2 + (32 + shiftY) * scale), xy(1, 1))

      draw_text({font: font, text: "⊲", pos: xy(-arrowX, arrowY) * scale + xy(0, 2), color: text_shadow_color, x_align: "center"})
      draw_text({font: font, text: "⊳", pos: xy(+arrowX, arrowY) * scale + xy(0, 2), color: text_shadow_color, x_align: "center"})
      draw_text({font: font, text: "⊲", pos: xy(-arrowX, arrowY) * scale, color: promptColor, x_align: "center"})
      draw_text({font: font, text: "⊳", pos: xy(+arrowX, arrowY) * scale, color: promptColor, x_align: "center"})

      draw_text({font: choiceFont, text: playerName[p] + control.type, shadow: text_shadow_color,
                pos: xy(0, -2) * scale, color: text_color, x_align: "center"})

      draw_sprite({sprite: controllerSpritesheet[control.type], pos: xy(0, 40 - shiftSpriteY) * scale})

      const shiftX = if SCREEN_SIZE.x == SCREEN_SIZE.y then 16 else 0
      draw_text({font: controlsFont, text: control.prompt["ⓠ"], pos: xy(-56 + shiftX, 20) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["⍐"], pos: xy(-56 + shiftX, 40) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["⍇"], pos: xy(-70 + shiftX, 52) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["⍈"], pos: xy(-42 + shiftX, 52) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["⍗"], pos: xy(-56 + shiftX, 64) * scale, color: #f, x_align: "center"})

      draw_text({font: controlsFont, text: control.prompt["ⓟ"], pos: xy(+56 - shiftX, 20) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["ⓓ"], pos: xy(+56 - shiftX, 40) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["ⓒ"], pos: xy(+42 - shiftX, 52) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["ⓑ"], pos: xy(+70 - shiftX, 52) * scale, color: #f, x_align: "center"})
      draw_text({font: controlsFont, text: control.prompt["ⓐ"], pos: xy(+56 - shiftX, 64) * scale, color: #f, x_align: "center"})


