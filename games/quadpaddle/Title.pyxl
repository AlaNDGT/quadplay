Title
============================================================================
const activeColor  = rgb(1,1,0)
const defaultColor = gray(60%)
const shadowColor  = rgba(0,0,0,50%)

let menuItem = 0

def startGame(numPlayers):
   score = 0
   firstBallLaunched = false
   level = 1
   timeLeft = 0
   scoreOffset = xy(0, 0)
   scoreColor  = defaultTextColor
   spareBalls = startingSpareBalls

   paddleArray = [
      make_entity({name: "P1 paddle", pos: xy(112, 213), angle:  0deg, sprite: paddleSprite[0][1], pad: gamepad_array[0], axis: "x"}),
      make_entity({name: "P2 paddle", pos: xy(213, 112), angle: 90deg, sprite: paddleSprite[0][2], pad: gamepad_array[0], axis: "y"}),
      make_entity({name: "P3 paddle", pos: xy(112,  11), angle:  0deg, sprite: paddleSprite[0][3], pad: gamepad_array[0], axis: "x"}),
      make_entity({name: "P4 paddle", pos: xy( 11, 112), angle: 90deg, sprite: paddleSprite[0][4], pad: gamepad_array[0], axis: "y"})
   ]

   createLevel(1)
   for P in paddleArray: P.pad = gamepad_array[0]
   if numPlayers ≥ 1: paddleArray[1].pad = gamepad_array[1]; paddleArray[3].pad = gamepad_array[1]
   if numPlayers ≥ 2: paddleArray[2].pad = gamepad_array[2]
   if numPlayers ≥ 3: paddleArray[3].pad = gamepad_array[3]
   
   set_mode(Play) because "# players selected"


frame
────────────────────────────────────────────────────────────────────────────
set_background(backgroundColor)

processParticles()
draw_sprite(logo[0][0], xy(SCREEN_SIZE.x / 2, SCREEN_SIZE.y / 4))

// Player options
let color
for p < 4:
   color = defaultColor
   if (menuItem == p): color = activeColor
   draw_text(font, (p + 1) + " player", xy(SCREEN_SIZE.x / 2 + 5, SCREEN_SIZE.y / 2 + p * 16), color, shadowColor, nil, 0, 0)

   // If any controller presses up or down, change the number of players
   if gamepad_array[p].yy:
      play_audio_clip(menuSound)
      menuItem = (menuItem + 6 + gamepad_array[p].yy) mod 6

// Credits option
color = defaultColor
if menuItem == 4: color = activeColor
draw_text(font, "Credits", xy(SCREEN_SIZE.x / 2 + 3, SCREEN_SIZE.y / 2 + 4 * 16), color, shadowColor, nil, "center", "middle")

color = defaultColor
if menuItem == 5: color = activeColor
draw_text(font, "Quit", xy(SCREEN_SIZE.x / 2 + 3, SCREEN_SIZE.y / 2 + 5 * 16), color, shadowColor, nil, "center", "middle")

draw_text(font, "✜", xy(SCREEN_SIZE.x / 2 - 29, SCREEN_SIZE.y / 2 + menuItem * 16), activeColor, shadowColor, nil, "center", "middle")

draw_text({font:tinyFont,
          text:"(C) 2019 Morgan McGuire | @CasualEffects | https://casual-effects.com",
          pos: xy(SCREEN_SIZE.x / 2, SCREEN_SIZE.y - 5),
          color: gray(60%),
          x_align:"center",
          y_align:"bottom",
          z:-2})

const radius = titleBall.size.x / 2
with pos, vel in titleBall:
   for i < 2: createParticle(pos + 5 xy(random() - 0.5, random() - 0.5), vel * random(), #ddd, 0, random() * 30 + 10)
   pos += vel
   // Bounce off walls
   if pos.x <= radius:               pos.x = radius;                    vel.x =  abs(vel.x); createTitleParticles()
   if pos.x > SCREEN_SIZE.x - radius: pos.x = SCREEN_SIZE.x - radius - 1; vel.x = -abs(vel.x); createTitleParticles()
   if pos.y <= radius:               pos.y = radius;                    vel.y =  abs(vel.y); createTitleParticles()
   if pos.y > SCREEN_SIZE.y - radius: pos.y = SCREEN_SIZE.y - radius - 1; vel.y = -abs(vel.y); createTitleParticles()

draw_entity(titleBall)

if any_button_press():
   play_audio_clip(selectSound)
   if menuItem < 4:         startGame(menuItem)
   else if menuItem == 4:   push_mode(Credits) because "Show credits selected"
   else:                    quit_game()
