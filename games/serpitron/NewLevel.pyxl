NewLevel
════════════════════════════════════════════════════════════════════════

def spawnPowerups():
   if level == 0:
      // First wave is always apples
      waveType = "Apples"
   else:
      // Spawn new wave
      const lastWaveType = waveType
      waveType = ∅
      while waveType == ∅ or waveType == lastWaveType:
         waveType = rndValue(waveTypeArray)
      
      // Score disparity increases the probability of a showdown
      if (size(snakeArray) > 1 and
          snakeArray[0].score > 400000 and
          snakeArray[0].score > 170% lastValue(snakeArray).score and
          (ξ < 35% or level == numLevels - 1) and
          lastWaveType != "Showdown"):
         waveType = "Showdown"


   // No showdowns in single-player games
   if size(snakeArray) == 1 and waveType == "Showdown": waveType = "Diamonds"

   for snake in snakeArray:
      snake.hasGun = false

   if waveType == "Apples":
      for i < 6 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.apple, type: "grow", pos: randomEmptyPosition(), 
                             value: growAmount, audioClip: bigGrowAudioClip})
   else if waveType == "Diamonds":
      for i < 4 size(snakeArray):
         push(powerupArray, {sprite: powerupSpritesheet.diamond, type: "points", pos: randomEmptyPosition(), 
                             value: pointBonus, audioClip: bigPointsAudioClip})
   else if waveType == "Cherries":
      for i < 16 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.cherry, type: "grow", pos: randomEmptyPosition(), 
                             value: ⌊¼ growAmount⌋, audioClip: smallGrowAudioClip})
   else if waveType == "Gold":
      for i < 18 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.coin, type: "points", pos: randomEmptyPosition(), 
                             value: ⌊¼ pointBonus⌋, audioClip: smallPointsAudioClip})         
   else if waveType == "Showdown":
      for i < 7 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.star, type: "points", pos: randomEmptyPosition(), 
                             value: ⌊¼ pointBonus⌋, audioClip: smallPointsAudioClip})
      for snake in snakeArray:
         snake.hasGun = (snake != snakeArray[0]) and (snake.score ≤ ½(snakeArray[0].score + lastValue(snakeArray).score))
         
   else if waveType == "Rats":
      for i < 60:
         push(powerupArray, {sprite: ratSpritesheet[0][0], type: "grow", pos: randomEmptyPosition(), 
                             value: growAmount, audioClip: bigGrowAudioClip,
                             angle: 90deg rndInt(0, 3),
                             speed: 0.25 rndInt(1, 3)})
   else if waveType == "Coffee Break":
      for i < 21 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.coffee, type: "speed", pos: randomEmptyPosition(), 
                             value: 2, audioClip: speedUpAudioClip})
      for i < 11 sqrt(size(snakeArray)):
         push(powerupArray, {sprite: powerupSpritesheet.donut, type: "speed", pos: randomEmptyPosition(), 
                             value: 0.5, audioClip: slowDownAudioClip})


         
def spawnBuildings():
   if locationName == "Country":
      return
   else if locationName == "Urban":
      const sprite = skyscraperSprite[0][0]
      for 0 < i < 16:
         let y = rndInt(sprite.size.y, playSize.y - sprite.size.y)
         if i == 4 or i == 5 or i == 12 or i == 11: y = playSize.y / 2
         push(buildingArray, {
            pos: xy(round(i * playSize.x / 16, cellSide), round(y, cellSide)),
               sprite: sprite, size: sprite.size, shape: "rect", shadowShape: "rect"})
            
   else if locationName == "Desert":
      const sprite = pyramidSprite[0][0]
      
      // center X, random 1/3 Y
      if ξ < 75%:
         push(buildingArray, {
            pos:    xy(round(playSize.x / 2, cellSide), round(rndInt(sprite.size.y, playSize.y / 2 - sprite.size.y / 2), cellSide)) + cellSide / 2,
            sprite: sprite, size: sprite.size, shape: "rect", shadowShape: "tri"})

      // center X, random 2/3 Y
      if ξ < 75%:
         push(buildingArray, {
            pos:    xy(round(playSize.x / 2, cellSide), round(rndInt(playSize.y / 2 + sprite.size.y / 2, playSize.y - sprite.size.y), cellSide)) + cellSide / 2,
            sprite: sprite, size: sprite.size, shape: "rect", shadowShape: "tri"})
         
      // random 1/3 X, center Y
      if ξ < 75%:
         push(buildingArray, {
            pos:    xy(round(rndInt(sprite.size.x, playSize.x / 2 - sprite.size.x), cellSide),
                       round(playSize.y / 2, cellSide)) + cellSide / 2,
            sprite: sprite, size: sprite.size, shape: "rect", shadowShape: "tri"})
         
      // random 2/3 X, center Y
      if ξ < 75%:
         push(buildingArray, {
            pos:    xy(round(rndInt(playSize.x / 2 + sprite.size.x, playSize.x - sprite.size.x), cellSide),
                       round(playSize.y / 2, cellSide)) + cellSide / 2,
            sprite: sprite, size: sprite.size, shape: "rect", shadowShape: "tri"})


         
def spawnWeather():
   if weatherType == "Blizzard":
      while size(weatherArray) < 50:
         push(weatherArray, makeEntity({
            pos: xy(rnd(-50, playSize.x + 50), rnd(-50, playSize.y + 50)),
            vel: xy(rnd(-0.5, 0.5), -rnd(0.1, 0.5)),
            sprite: rndValue(snowflakeSprite[0]),
            angle: rnd(0, 360deg),
            spin: rnd(-1deg, 1deg),
            scale: rnd(1, 2),
            z: 20
         }))
   else if weatherType == "Cloudy":
      while size(weatherArray) < 18:
         push(weatherArray, makeEntity({
            pos: xy(rnd(-50, playSize.x + 50), rnd(-50, playSize.y + 50)),
            vel: xy(rnd(-0.25, 0.25), -rnd(0.05, 0.05)),
            sprite: rndValue(cloudSprite[0]),
            spin: 0,
            scale: xy(rndSign() * rnd(1, 3), rnd(0.75, 1.5)),
            z: 20
         }))
         
         
enter
────────────────────────────────────────────────────────────────────────
++level
powerupArray = []
shotArray = []
buildingArray = []
weatherArray = []

for snake in snakeArray:
   moveToStartingPosition(snake)
   snake.hasGun = false

// No weather on the first two levels, to make it easier
weatherType = if level > 1 and ξ < 20% then rndValue(weatherNameArray) else ""
locationName = if level == 0 then "Country" else rndValue(locationNameArray)

location = locationTable[locationName]

spawnWeather()
spawnBuildings()
spawnPowerups()

shadowColor = 85% location.background
  
levelDescription = concat(locationName, weatherType, waveType)

if backgroundSound: stopSound(backgroundSound)
backgroundSound = playAudioClip(location.music, true)



frame
────────────────────────────────────────────────────────────────────────

drawPreviousMode()

// Pause for half a second on later levels
const delay = if level == 0 then 0 else 30

const t = max(modeFrames - delay, 0)

// Spin out
const k = t² / 200
setPostEffects({scale: 1 + max(0, k - 2), angle: -k, background: #a})

if modeFrames > 80:
   if size(snakeArray) > 1 and level >= numLevels:
      setMode(GameOver) because "Multiplayer levels done"
   else:
      setMode(LevelTitle) because "Animation end"
