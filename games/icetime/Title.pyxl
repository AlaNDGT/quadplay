Title
═══════════════════════════════════════════════════════════════════════════════

const choiceArray = ["1P", "2P-coop", "2P-vs", "3P", "4P", "Demo", "Credits", "Quit", "Rules"]
let selection = 5

let titleSound

// Choose random teams
teamArray = []
teamArray[0] = clone(rndValue(league))
teamArray[1] = createRandomDifferentTeam(teamArray[0])


enter
───────────────────────────────────────────────────────────────────────────────
resetTransform()
resetClip()
setBackground(labelSprite)
titleSound = playAudioClip(chantAudioClip, true, 150%)

setPostEffects({background:#0000, opacity: 50%})

const c = find(choiceArray, loadLocal("choice"))
if c ≠ ∅: selection = (c + 5) mod size(choiceArray)



leave
───────────────────────────────────────────────────────────────────────────────
stopSound(titleSound)
playAudioClip(shootAudioClip)
resetPostEffects();


frame
───────────────────────────────────────────────────────────────────────────────

drawSprite(titleSprite, xy(screenSize.x / 2, 48))

const N = size(choiceArray)
let choice

for i < size(choiceArray):
   const angle = 360 deg * (i - selection) / N + 90 deg
   const z = sin(angle)
   const a = (z + 3) / 4
   let color = gray(a)
   let border = gray(65% - 30% a)
   if i == round(selection) mod N: color = #ec0; border = #0; choice = choiceArray[i]
   drawText({font: scoreFont, text: choiceArray[i],
             pos: xy(50% screenSize.x, 65% screenSize.y) + xy(75 cos angle, 8 z),
             color: color, outline: border, z: z, xAlign: "center", yAlign: "center"})


drawText({font: tinyFont, text: "by Casual Effects", pos: xy(screenSize.x / 2, screenSize.y - 6), color: #5, xAlign: "center"})

// Spin animation in the intro
if modeFrames < 72:
   selection += mid(12%, 12% - modeFrames * 0.2%, 2%)
else if joy.aa or joy.bb:
   if choice == "Quit": quitGame()
   else if choice == "Rules": setMode(Rules)
   else if choice == "Credits": setMode(Credits)
   else:
      if choice == "Demo": controlPadArray = [makeAI(), makeAI(), makeAI(), makeAI()]
      else if choice == "1P": controlPadArray = [pad[0], makeAI(), makeAI(), makeAI()]
      else if choice == "2P-coop": controlPadArray = [pad[0], pad[1], makeAI(), makeAI()]
      else if choice == "2P-vs": controlPadArray = [pad[0], makeAI(), pad[1], makeAI()]
      else if choice == "3P": controlPadArray = [pad[0], pad[1], pad[2], makeAI()]
      else if choice == "4P": controlPadArray = [pad[0], pad[1], pad[2], pad[3]]

      if choice == "Demo":
         createGame()
         setMode(Faceoff) because "Demo Mode"
      else:
         saveLocal("choice", choice)
         setMode(TeamSelect) because "1-4P"

selection = loop(selection - 4% joy.x, N)

